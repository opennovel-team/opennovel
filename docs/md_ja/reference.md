OpenNovel コマンドリファレンス
==============================

## 索引

* [コマンドとは](#about)
* 画面表示系
    * [メッセージ](#message)
    * [セリフ](#serif)
    * [@bg 背景の表示](#bg)
    * [@ch キャラの表示](#ch)
    * [@chch 複数キャラと背景の一括変更](#chch)
    * [@move キャラの移動](#move)
    * [@shake 画面を揺らす](#shake)
    * [@video 動画の再生](#video)
    * [@anime アニメーションの表示](#anime)
    * [@layer レイヤへの画像読み込み](#layer)
    * [@pencil テキストレイヤへの描画](#pencil)
* 音声再生系
    * [@bgm BGMの再生](#bgm)
    * [@se SEの再生](#se)
    * [@vol ボリュームの変更](#vol)
* 選択肢表示系
    * [@choose 選択肢の表示](#choose)
    * [@ichoose インライン選択肢の表示](#ichoose)
    * [@mchoose 条件つき選択肢の表示](#mchoose)
    * [@michoose 条件つきインライン選択肢の表示](#michoose)
    * [@gui GUIの表示](#gui)
* 待機系
    * [@click クリックされるまで待機](#click)
    * [@wait 指定した時間だけ待機](#wait)
    * [@skip ウェイトのスキップを禁止](#skip)
* シナリオ分岐系
    * [ラベル](#label)
    * [@goto ラベルにジャンプ](#goto)
    * [@set 変数に値を設定](#set)
    * [@if 変数の値でジャンプ](#if)
    * [@load シナリオファイルにジャンプ](#load)
    * [@chapter 章のタイトルを設定](#chapter)
    * [@gosub マクロにジャンプ](#gosub)
    * [@return マクロから戻る](#return)
* システム系
    * [@config コンフィグの書き換え](#config)
    * [@wms WMSスクリプトの実行](#wms)
    * [using マクロの取り込み](#using)

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="about">
## コマンドとは

コマンドとは OpenNovel に対する指示のことです。
ほとんどのコマンドは `@` マークから始まります。
コマンドの名前を無理に覚える必要はなく、一度使ったものをコピペして使うなどしてください。

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="message">
## メッセージ

シナリオファイル中で日本語の文章を記述すると、そのテキストがそのままメッセージボックスに出力されます。
また、表示後、クリックされるまで待ちます。

### メッセージボックス

メッセージボックスが非表示の場合、メッセージコマンドはメッセージボックスを表示します。
メッセージボックスは、デフォルトでは背景表示のときに非表示になります。
メッセージボックスは、一度メッセージコマンドで表示されると、次に別なコマンドで非表示にされるまでは、表示状態が維持されます。

### 装飾

メッセージ中では下記の装飾が利用できます。

|記述           |装飾の内容                                                                    |
|---------------|------------------------------------------------------------------------------|
|\n             |改行する。                                                                    |
|$数字          |ローカル変数やグローバル変数の値の数値を出力する。                            |
|%アルファベット|名前変数の値の文字列を出力する。                                              |
|\#{RRGGBB}     |フォントの色を変更する。                                                      |
|\#{DEF}        |フォントの色をデフォルトに戻す。                                              |
|\@{サイズ}     |フォントのサイズを変更する。\{!サイズ}と書くとベースの高さ(下端)も変更できる。|
|\^{ルビ}       |ルビを振る。                                                                  |
|\f{1}          |フォント1を使用する。                                                         |
|\f{2}          |フォント2を使用する。                                                         |
|\f{3}          |フォント3を使用する。                                                         |
|\f{4}          |フォント4を使用する。                                                         |
|\o{+}          |フォントのふちどりをONにする。                                                |
|\o{-}          |フォントのふちどりをOFFにする。                                               |
|\p{x,y}        |描画位置を(x,y)に変更する。                                                   |
|\w{秒数}       |ウェイトを挿入する。                                                          |
|\M{左,上}      |@pencilで描画する場合に左方向と上方向のマージンを指定する。                   |
|\L{高さ}       |@pencilでの行間を指定する。                                                   |

### ノベル記法

コンフィグでノベル記法が有効な場合、メッセージはいったん一時領域に保存されて、`\---` の指示で一気に表示されます。

`\---` は表示の他にクリック待ちを行います。

`\===` はメッセージボックスをクリアして新しいページに移る指示です。

### 行の継続

行頭に `\` を付加すると、前の行に続けてメッセージを継続します。
同様に、行頭に `\\n` を付加すると、前の行を改行してメッセージを継続します。
いずれにせよ、改行でクリック待ちが入ります。

### メッセージを一瞬で表示するには

メッセージを一瞬で表示するための特別なコマンドはなく、一時的にコンフィグを書き換えて制御します。

```
@config msgbox.nowait yes
一瞬で表示するメッセージ
@config msgbox.nowait no
```

### 使い方

```
こんにちは。
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="serif">
## セリフ

```
キャラ名「セリフ」
*キャラ名*セリフ
*キャラ名*音声ファイル*セリフ
```

メッセージボックスにセリフのテキストを出力します。
また、名前ボックスにキャラクタの名前を出力します。

### 名前ボックス

名前ボックスはセリフの表示中に有効になります。

### ボイスファイル

セリフの表示と同時にボイスファイルを再生することができます。
ボイスファイルは、フォルダ `cv` の中に存在する必要があります。
ボイスファイルの形式は、44.1kHzのOgg Vorbisである必要があります。

### 装飾

メッセージとまったく同じ装飾が使えます。

### 使い方1

セリフを出力します。

```
キャラ名「こんにちは。」
```

別な書き方として次のものがあります。

```
*キャラ名*こんにちは。
```

### 使い方2

ボイスを再生して、セリフを出力します。

```
*キャラ名*001.ogg*こんにちは。
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="bg">
## @bg

```
@bg [ファイル名] (秒) (エフェクト)
@bg [file=ファイル名] (t=秒) (effect=エフェクト)
```

背景を変更します。

## キャラクタの退場

背景の変更後、キャラクタはステージ上からいなくなります。
キャラクタを維持したまま背景のみを変更するには、[@chch](#chch) コマンドを利用してください。

## メッセージボックスの非表示

デフォルトでは、背景の変更後、メッセージボックスが非表示になります。
この動作はコンフィグの `msgbox.show.on.bg` で変更できます。

## 背景ファイル

背景ファイルは `bg` フォルダに格納する必要があります。
ファイル形式は `PNG`, `JPEG`, `WebP` のいずれかである必要があります。

### 使い方1

`sample.png` を `1.5` 秒かけて背景にセットします。

```
@bg sample.png 1.5
```

### 使い方2

`sample.png` を即座に背景にセットします。

```
@bg sample.png
```

### 使い方3

`sample.png` を `1.5` 秒かけて右方向カーテンフェードで背景にセットします。

```
@bg sample.png 1.5 c
```

### 使い方4

色を指定して `1.5` 秒かけて左方向カーテンフェードで背景にセットします。

```
@bg #ff8080 1.5 curtain-left
```

### 使い方5

ルール画像を使ってトランジションを行います。
ルール画像はグレースケールで、黒い部分から白い部分にかけて順に描画されます。

```
@bg sample.png 1.5 rule:star.png
```

### 背景/キャラで共通のエフェクト

フェードには次のエフェクトを指定できます。

|エフェクトの種類               |エフェクト指定          |短縮名1|短縮名2|短縮名3|
|-------------------------------|------------------------|-------|-------|-------|
|標準のアルファブレンド         |normal                  |n      |       |       |
|ルール指定トランジション(1bit) |rule:ルール画像ファイル |       |       |       |
|ルール指定トランジション(8bit) |melt:ルール画像ファイル |       |       |       |
|右方向カーテン                 |curtain-right           |cr     |curatin|c      |
|左方向カーテン                 |curtain-left            |cl     |       |       |
|上方向カーテン                 |curtain-up              |cu     |       |       |
|下方向カーテン                 |curtain-down            |cd     |       |       |
|右方向スライド                 |slide-right             |sr     |       |       |
|左方向スライド                 |slide-left              |sl     |       |       |
|上方向スライド                 |slide-up                |su     |       |       |
|下方向スライド                 |slide-down              |sd     |       |       |
|右方向シャッター               |shutter-right           |shr    |       |       |
|左方向シャッター               |shutter-left            |shl    |       |       |
|上方向シャッター               |shutter-up              |shu    |       |       |
|下方向シャッター               |shutter-down            |shd    |       |       |

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="ch">
## @ch

```
@ch [位置] [ファイル名] (秒) (エフェクト) (Xオフセット) (Yオフセット) (アルファ値)
@ch [pos=位置] [file=ファイル名] (t=秒) (effect=エフェクト) (right=Xオフセット) (down=Yオフセット) (alpha=アルファ値)
```

キャラクタを表示、変更、消去します。

### 目パチと口パク

[目パチ](eye-blink.html) と [口パク](lip-sync.html) に対応しています。

### キャラクタの位置

キャラクタの位置には次のものがあります。

|表示位置       |コマンドでの指定方法  |短縮形  |
|---------------|----------------------|--------|
|左             |left                  |l       |
|左中央         |left-center           |lc      |
|中央           |center                |c       |
|右中央         |right-center          |rc      |
|右             |right                 |r       |
|背面中央       |back                  |b       |
|顔             |face                  |f       |

### エフェクトの種類

エフェクトの種類は [@bg](#bg) と同じです。

### 使い方1

`sample.png` を `0.5` 秒かけて中央前面にセットします。

```
@ch center sample.png 0.5
```

### 使い方2

`center` は `c` と省略して構いません。
また、ファイル名の拡張子は省略できます。

```
@ch c sample 0.5
```

### 使い方3

`none<` を指定するとキャラクタを退場させます。

```
@ch c none 0.5
```

### 使い方4

時間を指定しないと即座に表示されます。

```
@ch c sample
```

### 使い方5

エフェクトに <code>normal</code> あるいは省略形の <code>n</code> を指定すると、通常のアルファブレンドが用いられます。
指定を省略したときにも通常のアルファブレンドが用いられます。

```
@ch c sample.png 1.0 normal
```

### 使い方7

キャラクタ表示位置をずらすことができます。
下記の例では右に100ピクセル、下に50ピクセルずらします。

```
@ch c sample.png 1.0 n 100 50
```

### 使い方8

キャラクタのアルファ値を指定することができます。
アルファ値には `0` から `255` を指定できます。
また、アルファ値には `255` の別名で `show` を指定できるほか、`0` の別名で `hide` を指定できます。

```
@ch c sample.png 1.0 n 0 0 show
```

### 使い方9

メッセージボックスの上にキャラの顔を表示します。
消す場合はファイル名にnoneを指定します。

```
@ch face face-001.png
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="chch">
## @chch

```
@chch 引数は自由
```

キャラクタの一括変更を行います。
背景のみの変更や、キャラクタと同時に背景も変更できます。

### より頻繁な演出を行うには

アニメーションを含めたより頻繁な演出を行う場合は、@chch の代わりに [Ciel](ciel.html) を利用してください。

### 引数

このコマンドに限って、`center=` のような引数の名前が必須です。
引数の指定順に制限がありません。
引数の一覧は以下です。

|指定                         |引数             |短縮形 |
|-----------------------------|-----------------|-------|
|中央キャラ画像               |center=          |c=     |
|中央キャラのXオフセット      |center-x=        |cx=    |
|中央キャラのYオフセット      |center-y=        |cy=    |
|中央キャラのアルファ         |center-a=        |ca=    |
|中央キャラを暗くする         |center-dim=      |cd=    |
|右キャラ画像                 |right=           |r=     |
|右キャラのXオフセット        |right-x=         |rx=    |
|右キャラのYオフセット        |right-y=         |ry=    |
|右キャラのアルファ           |right-a=         |ra=    |
|右キャラを暗くする           |right-dim=       |rd=    |
|右中キャラ画像               |right-center=    |rc=    |
|右中キャラのXオフセット      |right-center-x=  |rcx=   |
|右中キャラのYオフセット      |right-center-y=  |rcy=   |
|右中キャラのアルファ         |right-center-a=  |rca=   |
|左キャラ画像                 |left=            |l=     |
|左キャラのXオフセット        |left-x=          |lx=    |
|左キャラのYオフセット        |left-y=          |ly=    |
|左キャラのアルファ           |left-a=          |la=    |
|左キャラを暗くする           |left-dim=        |ld=    |
|左中キャラ画像               |left-center=     |lc=    |
|左中キャラのXオフセット      |left-center-x=   |lcx=   |
|左中キャラのYオフセット      |left-center-y=   |lcy=   |
|左中のアルファ               |left-center-a=   |lca=   |
|左中キャラを暗くするか       |left-center-dim= |lcd=   |
|中央背面キャラ画像           |back=            |b=     |
|中央背面キャラのXオフセット  |back-x=          |bx=    |
|中央背面キャラのYオフセット  |back-y=          |by=    |
|中央背面キャラのアルファ     |back-a=          |ba=    |
|背景画像                     |background=      |bg=    |
|背景のXオフセット            |bg-x=            |bgx=   |
|背景のYオフセット            |bg-y=            |bgy=   |
|エフェクト                   |effect=          |e=     |
|演出時間                     |t=               |       |

### 変更のないキャラクタ

変更のないキャラクタは指定しなくて構いません。

### キャラクタの退場

退場させるキャラには `center=none` のように `none` を指定します。

### エフェクトの種類

エフェクトの種類は [@bg](#bg) と同じです。

### オフセット位置の指定

オフセットはキャラ画像の変更と同時に初期位置に戻されます。
変更済みのオフセットを維持しつつ画像だけを変更する場合、オフセット指定に `cx=keep` のように指定します。

### 使い方1

中央と右にキャラクタを `1.0` 秒かけて表示します。
他のキャラクタは変更しません。

```
@chch center=center.png right=right.png t=1.0
```

### 使い方2

中央のキャラクタを `1.0` 秒かけて退場させます (`none` の指定)。
他のキャラクタは変更しません。

```
@chch center=none t=1.0
```

### 使い方3

キャラクタを表示させたまま背景を `1.0` 秒かけて変更します。

```
@chch bg=background.png t=1.0
```

### 使い方4

中央のキャラクタを `1.0` 秒かけて変更します。
背景も変更します。
エフェクトはカーテンです。

```
@chch center=center.png bg=background.png effect=curtain t=1.0
```

### 使い方5

中央のキャラクタを `1.0`> 秒かけて変更します。
背景も変更します。
キャラクタのX位置とY位置は前に指定していたものを継続します。
引数は省略形を用いています。

```
@chch bg=bg.png c=center.png cx=keep cy=keep t=1.0
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="move">
## @move

```
@move [位置] [秒] [加速方法] [X移動量] [Y移動量] [アルファ値]
@move [pos=位置] [t=秒] [accel=加速方法] [x=X移動量] [y=Y移動量] [alpha=アルファ値]
```

アニメーションを用いてキャラクタの移動を行います。
内部的にアニメファイルが生成されるため、簡易的な演出を利用できます。

### キャラクタの位置

キャラクタの位置の指定は [@ch](#ch) を参照してください。

### 加速方法

加速方法には以下の指定が利用できます。

|名前　|意味              |
|------|------------------|
|move  |等速で動かします。|
|accel |動きを加速します。|
|brake |動きを減速します。|

### アルファ値

アルファ値の指定は [@ch](#ch) を参照してください。
`hide` や `show` も利用できます。

### 使い方1

中央に表示したキャラクタを <code>1.0</code> 秒かけて左側に <code>600</code> ピクセル移動して非表示にします。

```
@move center 1.0 move -600 0 hide
```

### 使い方2

使い方1との差分は、移動を加速(アクセル)しながら行うことです。

```
@move center 1.0 accel -600 0 hide
```

### 使い方3

使い方1との差分は、移動を減速(ブレーキ)しながら行うことです。

```
@move center 1.0 brake -600 0 hide
```

### 使い方4

初めに画面の外にキャラクタを読み込んでおいて、画面の中に移動させます。

```
@ch right sample.png 0 n 600 0 hide
@move right 2.0 move -600 0 show
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="shake">
## @shake

```
@shake [方向] [秒数] [振動回数] [大きさ]
@shake [dir=方向] [t=秒数] [count=振動回数] [amp=大きさ]
```

画面を揺らします。

### 使い方1

`1.0` 秒かけて、`3` 回、横方向に `100` px、画面を揺らします。
なお、横方向という意味の `horizontal` は `h` と省略できます。

```
@shake horizontal 1.0 3 100
```

### 使い方2

`1.0` 秒かけて、`3` 回、縦方向に `100` px、画面を揺らします。
なお、縦方向という意味の `vertical` は `v` と省略できます。

```
@shake vertical 1.0 3 100
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="video">
## @video

```
@video [ファイル名]
@video [file=ファイル名]
```

動画を再生します。

Windowsで再生できる動画は`.wmv` ファイルです。
その他の環境で再生できる動画は `.mp4` ファイルです。
ファイル名の指定では拡張子 (`.wmv` や `.mp4`) を省略すれば、
その環境用にファイルが自動選択されます。

動画ファイルは `mov` ディレクトリに格納します。
`mov` ディレクトリは `data01.arc` ファイルにパッケージングされません。

Webブラウザでは、プレイヤが最初にクリックを行う前に動画を再生しようとすると、セキュリティの違反となりゲームが止まります。
Web版でゲームの先頭で動画を再生するときは、事前に `@click` コマンドを入れるなどの対策を行ってください。

### 使い方

動画を再生します。
Windowsでは `sample.wmv` が再生されます。
その他の環境では `sample.mp4` が再生されます。

```
@video sample
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="anime">
## @anime

```
@anime [ファイル名] (オプション群)
@anime [file=ファイル名] (オプション群)
```

アニメファイルを実行します。

### アニメとは

アニメはレイヤの移動・拡大・縮小・回転を行う機能です。
アニメファイルはフォルダ `anime` に配置する必要があります。

デフォルトでは @anime コマンドはアニメファイルが完了するまで待ちます。
`async` オプションをつけるとこで完了を待たずに次のコマンドに進むことができます。 

### アニメファイルの記述方法

アニメファイルの記述方法については [アニメ](anime.html) をご参照ください。

### レイヤへの画像の読み込み

レイヤへの画像の読み込みはアニメファイルではできませんので、[@layer](#layer) コマンドを利用してください。

### アニメの終了

ファイル名に `clear` を指定すると、オプションで指定したレイヤのアニメを終了します。

### レイヤ状態のリセット

ファイル名に `reset` を指定すると、オプションで指定したレイヤの、オプションで指定した項目を、初期値に戻します。

### オプション

オプションには次のものが指定できます。半角コンマで区切ることで複数指定できます。

|オプション名     |意味                                                                                                                                   |
|-----------------|---------------------------------------------------------------------------------------------------------------------------------------|
|async            |アニメの終了を待たずに次のコマンドに進みます。（非同期アニメ）                                                                         |
|nosysbtn         |アニメ中にシステムボタンを表示しません。                                                                                               |
|showmsgbox       |アニメ中にメッセージボックスを表示します。                                                                                             |
|forcemsgbox      |非表示状態のメッセージボックスを強制的に表示します。                                                                                   |
|forcenamebox     |非表示状態の名前ボックスを強制的に表示します。                                                                                         |
|layer-all        |ファイル名に `clear` か `reset` を指定した際、すべてのレイヤを対象にします。                                                           |
|layer-bg         |ファイル名に `clear` か `reset` を指定した際、`bg` (背景) レイヤを対象にします。                                                       |
|layer-bg2        |ファイル名に `clear` か `reset` を指定した際、`bg2` (背景2) レイヤを対象にします。                                                     |
|layer-effect5    |ファイル名に `clear` か `reset` を指定した際、`effect5` レイヤを対象にします。                                                         |
|layer-effect6    |ファイル名に `clear` か `reset` を指定した際、`effect6` レイヤを対象にします。                                                         |
|layer-effect7    |ファイル名に `clear` か `reset` を指定した際、`effect7` レイヤを対象にします。                                                         |
|layer-effect8    |ファイル名に `clear` か `reset` を指定した際、`effect8` レイヤを対象にします。                                                         |
|layer-chb        |ファイル名に `clear` か `reset` を指定した際、`chb` (背面中央キャラ) レイヤを対象にします。                                            |
|layer-chb-eye    |ファイル名に `clear` か `reset` を指定した際、`chb-eye` (背面中央キャラの目パチ) レイヤを対象にします。                                |
|layer-chb-lip    |ファイル名に `clear` か `reset` を指定した際、`chb-lip` (背面中央キャラの口パク) レイヤを対象にします。                                |
|layer-chl        |ファイル名に `clear` か `reset` を指定した際、`chl` (左キャラ) レイヤを対象にします。                                                  |
|layer-chl-eye    |ファイル名に `clear` か `reset` を指定した際、`chl-eye` (左キャラの目パチ) レイヤを対象にします。                                      |
|layer-chl-lip    |ファイル名に `clear` か `reset` を指定した際、`chl-lip` (左キャラの口パク) レイヤを対象にします。                                      |
|layer-chlc       |ファイル名に `clear` か `reset` を指定した際、`chlc` (左中央キャラ) レイヤを対象にします。                                             |
|layer-chlc-eye   |ファイル名に `clear` か `reset` を指定した際、`chlc-eye` (左中央キャラの目パチ) レイヤを対象にします。                                 |
|layer-chlc-lip   |ファイル名に `clear` か `reset` を指定した際、`chlc-lip` (左中央キャラの口パク) レイヤを対象にします。                                 |
|layer-chr        |ファイル名に `clear` か `reset` を指定した際、`chr` (右キャラ) レイヤを対象にします。                                                  |
|layer-chr-eye    |ファイル名に `clear` か `reset` を指定した際、`chr-eye` (右キャラの目パチ) レイヤを対象にします。                                      |
|layer-chr-lip    |ファイル名に `clear` か `reset` を指定した際、`chr-lip` (右キャラの口パク) レイヤを対象にします。                                      |
|layer-chrc       |ファイル名に `clear` か `reset` を指定した際、`chrc` (右中央キャラ) レイヤを対象にします。                                             |
|layer-chrc-eye   |ファイル名に `clear` か `reset` を指定した際、`chrc-eye` (右中央キャラの目パチ) レイヤを対象にします。                                 |
|layer-chrc-lip   |ファイル名に `clear` か `reset` を指定した際、`chrc-lip` (右中央キャラの口パク) レイヤを対象にします。                                 |
|layer-chc        |ファイル名に `clear` か `reset` を指定した際、`chc` (中央キャラ) レイヤを対象にします。                                                |
|layer-chc-eye    |ファイル名に `clear` か `reset` を指定した際、`chc-eye` (中央キャラの目パチ) レイヤを対象にします。                                    |
|layer-chc-lip    |ファイル名に `clear` か `reset` を指定した際、`chc-lip` (中央キャラの口パク) レイヤを対象にします。                                    |
|layer-effect1    |ファイル名に `clear` か `reset` を指定した際、`effect1` レイヤを対象にします。                                                         |
|layer-effect2    |ファイル名に `clear` か `reset` を指定した際、`effect2` レイヤを対象にします。                                                         |
|layer-effect3    |ファイル名に `clear` か `reset` を指定した際、`effect3` レイヤを対象にします。                                                         |
|layer-effect4    |ファイル名に `clear` か `reset` を指定した際、`effect4` レイヤを対象にします。                                                         |
|layer-msg        |ファイル名に `clear` か `reset` を指定した際、`msg` (メッセージボックス) レイヤを対象にします。                                        |
|layer-name       |ファイル名に `clear` か `reset` を指定した際、`name` (名前ボックス) レイヤを対象にします。                                             |
|layer-chf        |ファイル名に `clear` か `reset` を指定した際、`chf` (キャラ顔) レイヤを対象にします。                                                  |
|layer-chf-eye    |ファイル名に `clear` か `reset` を指定した際、`chf-eye` (キャラ顔の目パチ) レイヤを対象にします。                                      |
|layer-chf-lip    |ファイル名に `clear` か `reset` を指定した際、`chf-lip` (キャラ顔の口パク) レイヤを対象にします。                                      |
|layer-click      |ファイル名に `clear` か `reset` を指定した際、`click` (クリックアニメ) レイヤを対象にします。                                          |
|layer-auto       |ファイル名に `clear` か `reset` を指定した際、`auto` (オートモードバナー) レイヤを対象にします。                                       |
|layer-skip       |ファイル名に `clear` か `reset` を指定した際、`skip` (スキップモードバナー) レイヤを対象にします。                                     |
|layer-text1      |ファイル名に `clear` か `reset` を指定した際、`text1` レイヤを対象にします。                                                           |
|layer-text2      |ファイル名に `clear` か `reset` を指定した際、`text1` レイヤを対象にします。                                                           |
|layer-text3      |ファイル名に `clear` か `reset` を指定した際、`text1` レイヤを対象にします。                                                           |
|layer-text4      |ファイル名に `clear` か `reset` を指定した際、`text1` レイヤを対象にします。                                                           |
|layer-text5      |ファイル名に `clear` か `reset` を指定した際、`text1` レイヤを対象にします。                                                           |
|layer-text6      |ファイル名に `clear` か `reset` を指定した際、`text1` レイヤを対象にします。                                                           |
|layer-text7      |ファイル名に `clear` か `reset` を指定した際、`text1` レイヤを対象にします。                                                           |
|layer-text8      |ファイル名に `clear` か `reset` を指定した際、`text1` レイヤを対象にします。                                                           |
|layer-xy         |ファイル名に `reset` を指定した際、座標をリセット対象にします。                                                                        |
|layer-rotate     |ファイル名に `reset` を指定した際、回転をリセット対象にします。                                                                        |
|layer-scale      |ファイル名に `reset` を指定した際、拡大をリセット対象にします。                                                                        |
|reg00            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg01            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg02            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg03            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg04            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg05            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg06            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg07            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg08            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg09            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg10            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg11            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg12            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg13            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg14            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|
|reg15            |再生するアニメを登録番号0とし、セーブ・ロードで復元されるようにします。ファイル名に `unregister` が指定された場合は、登録を解除します。|

### 使い方1

`anime/anime1.txt` を再生します。再生完了まで待ちます。

```
@anime anime1.txt
```

### 使い方2

`anime/anime1.txt` を再生します。
再生完了を待たず、次のコマンドに進み、メッセージを表示します。(async 指定)
最後に、裏側で動いているアニメがすべて終了するのを待ちます。

```
@anime anime1.txt async
裏側で表示するメッセージを書きます。
@anime finish-all
```

### 使い方3

`anime/anime1.txt` を再生します。
オプションの `nosysbtn` により、アニメ再生中にシステムボタンを表示しません。

```
@anime anime1.txt nosysbtn
```

### 使い方4

anime/anime1.txt を再生します。
オプションの `showmsgbox` と `shownamebox` により、アニメ再生中にメッセージボックスを表示します。

```
@anime anime1.txt showmsgbox,shownamebox
```

### 使い方5

ループアニメ `anime/loop-anime1.txt` を再生します。

オプションの `reg00` により、登録アニメの `0` に登録します。
登録アニメはセーブ・ロード後に復元されます。
登録番号には、`reg00`, `reg01`, ..., `reg15` が利用できます。

ループアニメの再生が終了した際、ファイル名に `unregister` を指定して登録を解除しています。

```
@anime loop-anime1.txt async,reg00
アニメが裏側で動いています。
@anime clear layer-all
アニメが終了しました。
@anime unregister reg00</code></pre>
登録を解除しました。
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="layer">
## @layer

```
@layer [レイヤ名] [ファイル名] [X] [Y] [alpha]
@layer [name=レイヤ名] [file=ファイル名] [x=X] [y=Y] [a=アルファ]
```

レイヤに画像を読み込みます。
次の表示系コマンドに進むまで画面には反映されません。

### 画像の削除

レイヤーから画像を削除するには、ファイル名に `none` を指定します。

### レイヤーの種類

指定できるレイヤ名は下記です。

|レイヤ名       |
|---------------|
|bg             |
|bg2            |
|effect5        |
|effect6        |
|effect7        |
|effect8        |
|chb            |
|chb-eye        |
|chb-lip        |
|chl            |
|chl-eye        |
|chl-lip        |
|chlc           |
|chlc-eye       |
|chlc-lip       |
|chr            |
|chr-eye        |
|chr-lip        |
|chrc           |
|chrc-eye       |
|chrc-lip       |
|chc            |
|chc-eye        |
|chc-lip        |
|effect1        |
|effect2        |
|effect3        |
|effect4        |
|text1          |
|text2          |
|text3          |
|text4          |
|text5          |
|text6          |
|text7          |
|text8          |

### 使い方1

レイヤに画像をロードします。

```
@layer name=text1 file=switch-bg.png x=0 y=0 a=255
```

### 使い方2

レイヤの画像を削除します。

```
@layer name=text1 file=none
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="pencil">
## @pencil

```
@pencil [レイヤ] [テキスト]
@pencil [layer=レイヤ] [text=テキスト]
```

テキストレイヤにテキストを描画します。
文字を描画するテキストレイヤには、あらかじめ [@layer](#layer) コマンドで画像を読み込んでおく必要があります。

### 使い方

テキストレイヤ1 `text1` にテキストを描画します。

```
@layer name=text1 file=text-layer-image.png x=0 y=0 a=255
@text text1 "このテキストが描画されます。"
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="bgm">
## @bgm

```
@bgm [ファイル名] (once)
@bgm [file=ファイル名] (once)
```

BGMを再生します。

### ループ再生
BGMは自動的にループ再生されます。

### 音声ファイル
BGMファイルはフォルダ `bgm` に格納する必要があります。
再生可能なファイル形式は、44.1kHzのOgg Vorbisのみです。
もし特定の音楽ファイルが遅く聞こえたり、早く聞こえたりした場合は、そのファイルのサンプリングレートを44.1kHzに変換してください。

### 使い方1

`sample.ogg` を再生します。

```
@bgm sample.ogg
```

### 使い方2

BGMを停止します。

```
@bgm stop
```

### 使い方3

オプションの `once` により、BGMをループしないで1回だけ再生します。

```
@bgm sample.ogg once
```

### 応用例

BGMを2秒かけてフェードアウトします。

```
# 音量を2秒かけて0にする
@vol bgm 0 2

# 音量が0になるまで2秒待つ
@time 2

# 音量が0になっているBGMを実際に停止する
@bgm stop

# 音量を100%に戻す
@vol bgm 1.0 0
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="se">
## @se

```
@se [ファイル名] (loop)
@se [file=ファイル名] (loop)
```

SE(効果音)を再生します。

### 音声ファイル

SEファイルはフォルダ `se` に格納する必要があります。
再生可能なファイル形式は、44.1kHzのOgg Vorbisのみです。

### ループ再生

オプションの `loop` を指定するとループ再生します。

### 使い方1

SEを再生します。

```
@se click.ogg
```

### 使い方2

SEを停止します。

```
@se stop
```

### 使い方3

オプションの `loop` によりSEをループ再生します。

```
@se sample.ogg loop
```

### 特殊な使い方

ボイストラックでSEを再生します。
音声ファイルは `cv` フォルダのものが用いられます。

```
@se click.ogg voice
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="vol">
## @vol

```
@vol [トラック名] [音量] (秒)
@vol [track=トラック名] [vol=音量] (t=秒)
```

時間を指定してボリュームを調整します。

### トラックの種類

トラックの種類は下記の通りです。

|トラック名|省略形 |用途         |
|----------|-------|-------------|
|bgm       |b      |BGMの音量    |
|se        |s      |SEの音量     |
|voice     |v      |ボイスの音量 |

### 音量の値

音量の値は `0.0` から `1.0` の範囲で指定できます。
`0.0` が無音で、`1.0` が最大音量です。

音量は初期値で `1.0` で、セーブデータごとに別な値に変化します。

### 使い方

BGMの音量を `1.0` 秒かけて `0.5` にします。
音量の変化に `1.0` 秒かかるので、`@wait` コマンドを使用して `1.0` 秒の間、待機しています。

```
@vol bgm 0.5 1.0
@wait 1.0
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="choose">
## @choose

```
@choose [行き先1] [選択肢1] (行き先2) (選択肢2) ... (行き先10) (選択肢10)
@choose [l1=行き先1] [t1=選択肢] (l2=行き先2) (t2=選択肢2) ... (l10=行き先10) (t10=選択肢10)
```

選択肢を表示してラベルにジャンプします。
選択肢の数は最大で10個です。

### 使い方1

3つの選択肢を表示します。

```
@choose label1 おはよう label2 こんにちは label3 こんばんは

:label1
おはよう。
@goto end

:label2
こんにちは。
@goto end

:label3
こんばんは。
:end
```

### 使い方2

ラベルを書くのが面倒な場合、拡張switch文を利用できます。

```
<<<
switch "おはよう" "こんにちは" "こんばんは" {
case "おはよう":
    @ch center good-morning.png
    おはよう。
    break
case "こんにちは":
    @ch center good-afternoon.png
    こんにちは。
    break
case "こんばんは":
    @ch center good-evening.png
    こんばんは。
    break
}
>>>
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="ichoose">
## @ichoose

```
@ichoose [行き先1] [選択肢1] (行き先2) (選択肢2) ... (行き先10) (選択肢10)
@ichoose [l1=行き先1] [t1=選択肢1] (l2=行き先2) (t2=選択肢2) ... (l10=行き先10) (t10=選択肢10)
```

メッセージボックス内に選択肢を表示してラベルにジャンプします。

選択肢をメッセージボックス内に表示する以外は、[@choose](#choose) と同じです。

この機能を使用するときには、コンフィグの `choose.bg.image` と `choose.fg.image` に透明な画像を指定してください。

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="mchoose">
## @mchoose

```
@mchoose [変数1] [行き先1] [選択肢1] (変数2) (行き先2) (選択肢2) ... (変数10) (行き先10) (選択肢10)
```

対応する変数が `0` でない選択肢を表示します。
その他の機能は [@choose](#choose) と同じです。

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="michoose">
## @michoose

```
@michoose [変数1] [行き先1] [選択肢1] (変数2) (行き先2) (選択肢2) ... (変数10) (行き先10) (選択肢10)
```

対応する変数が `0` でない選択肢をメッセージボックス内に表示します。
その他の機能は [@ichoose](#ichoose) と同じです。

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="gui">
## @gui

```
@gui [ファイル名]
@gui [file=ファイル名]
```

GUIファイルを用いてメニューを表示します。

### GUIファイルの書き方

GUIファイルの書き方は [GUI](gui.html) をご覧ください。

### 使い方1

GUIファイル `menu.txt` を読み込んでメニューを実行します。

```
@gui menu.txt
```

### 使い方2

ロード画面を表示します。

```
@gui load.txt
```

### 使い方3

コンフィグ画面を表示します。

```
@menu config.txt
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="click">
## @click

```
@click
```

クリックされるのを待ちます。

### 待機中の画面表示

待っている間、メッセージボックスは非表示となります。

### 使い方

クリックを待ちます。

```
@click
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="wait">
## @wait

```
@wait [秒]
@wait [t=秒]
```

時間を指定して待機します。
待機中にキーボードやマウスの入力があれば待機を終了します。
デフォルトでは、待機中、メッセージボックスが表示されません。

### 使い方1

`5` 秒待ちます。

```
@wait 5.0
```

### 使い方2

`5` 秒待ちます。待機中、メッセージボックスと名前ボックスを表示します。

```
@time 5.0 showmsgbox,shownamebox
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="skip">
## @skip

```
@skip [enable/disable]
```

待ち時間のあるコマンドのスキップを禁止・解禁します。
主に、ゲーム起動時にブランドロゴを表示するのに使用できます。

### 使い方1

スキップを有効にします。 [デフォルト設定]

```
@skip enable
```

### 使い方2

スキップを無効にします。

```
@skip disable
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="label">
## @label

```
:ラベル名
```

ジャンプ先となるラベルを作成します。
ラベルはゲームの見た目には影響しません。
ラベルの名前には日本語を使うことも可能です。

なお、拡張if文や拡張switch文を使うことで、ラベルを使わずにゲームを記述できます。

### 使い方

ループします。

```
:ジャンプ先
何かメッセージを表示する
@goto ジャンプ先
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="goto">
## @goto

```
@goto [ラベル名]
```

ラベルにジャンプします。

### 使い方1

ラベル `AAA` にジャンプします。

```
@goto AAA
...
...
:AAA
ここにジャンプします。
```

### 使い方2

ループします。

```
:LOOP
...
...
@goto LOOP
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="set">
## @set

```
@set [変数] [演算子] [値]
```

ローカル変数、グローバル変数、名前変数に値をセットします。

### 変数の種類

通常の数値を記録するローカル変数、実績解除の数値を記録するグローバル変数、文字列を記録する名前変数があります。

|変数の種類      |説明                                                                                                                      |
|----------------|--------------------------------------------------------------------------------------------------------------------------|
|ローカル変数    |セーブデータごとに値が異なり、`$0` から `$9999` までの `10000` 個を利用できます。初期値は `0` です。                     |
|グローバル変数  |全てのセーブデータで共通の値となり、`$10000` から `$10999` までの `1000` 個を利用できます。初期値は `0` です。            |
|名前変数        |セーブデータごとに値が異なり、`%a` から `%z` までの `26` 個を利用できます。初期値は空の文字列です。                       |

### 計算の種類

`@set` で利用できる計算には下記のものがあります。
（名前変数には代入以外使えません。）

|計算の種類|意味                                |
|----------|------------------------------------|
|`=`       |単純に a に b を入れます(代入)      |
|`+=`      |a に b を足し算します(加算)         |
|`-=`      |a から b を引き算します(減算)       |
|`*=`      |a に b をかけ算します(乗算)         |
|`/=`      |a を b で割り算します(除算)         |
|`%=`      |a を b で割った余りを求めます(剰余) |

### 使い方1

ローカル変数 `$0` に値 `1` を代入します。
フラグのセットに利用します。
ローカル変数の初期値は `0` なので、フラグが立ったことになります。

```
@set $0 = 1
```

### 使い方2

ローカル変数 `$10` に値 `23` を加算します。
好感度の加算などに応用できます。

```
@set $10 += 23
```

### 使い方3

ローカル変数 `$0` に `0` から `9` までのランダムな値を代入します。
最初にランダムな数を `$RAND` で求めてから、`10` で割った余りを計算しています。
ランダムなイベント発生に応用できます。

```
@set $0 = $RAND
@set $0 %= 10
```

### 使い方4

ローカル変数 `$1` にローカル変数 `$2` を加算します。
好感度の計算などに利用できます。

```
@set $1 += $2
```

### 使い方5

名前変数 `%a` に文字列 `なまえ` を代入します。
デフォルトの名前を設定するのに利用できます。

```
@set %a = なまえ
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="if">
## @if

```
@if [変数名] [条件式] [値] [ラベル名]
```

条件式とマッチするときだけラベルにジャンプします。

### 拡張if文

拡張if文を使えば、ラベルなしで分岐が行えます。
使い方2をご覧ください。

### 条件の種類

利用できる条件の種類は下記の通りです。

|条件の種類  |意味                               |
|------------|-----------------------------------|
|`>`         |a > b (aがbより大きい)             |
|`>=`        |a ≧ b (aがbより大きいか等しい)    |
|`==`        |a = b (aとbが等しい)               |
|`<=`        |a ≦ b (aがbより小さいか等しい)    |
|`<`         |a < b (aがbより小さい)             |
|`!=`        |a ≒ b (aとbが等しくない)          |

### 条件文の左側の指定(左辺値)

条件文の左側はローカル変数名(`$0` など)か、
グローバル変数名(`$10000` など)か、
名前変数名(`%a` など)である必要があります。

### 条件文の右側の指定(右辺値)

条件文の左側がローカル変数名かグローバル変数名のとき、
条件文の右側は整数(`1` など)か、
ローカル変数名(`$0` など)か、
グローバル変数名(`$10000` など)を指定できます。

条件文の左側が名前変数のとき、条件文の右側には文字列のみ指定できます。
(文字列中の変数名は展開されません。)

### 使い方1

ローカル変数の値でシナリオを分岐します。
ローカル変数 `$1` の値が `1` であれば、ラベル `abc` にジャンプします。
つまり、フラグが立っていればシナリオを分岐します。

```
@if $1 == 1 abc
変数の値が1以外です。つまり、フラグが立っていないということです。
ここにシナリオを記述します。
@goto end

:abc
変数の値が1です。つまり、フラグが立っているということです。
ここにシナリオを記述します。

:end
```

### 使い方2

ラベルやジャンプがわかりにくいときは、拡張 if 文を使うことができます。

```
<<<
if $1 == 0 {
    おはよう。
}-
else if $1 == 1 {
    こんにちは。
}-
else {
    こんばんは。
}
>>>
```

### 使い方3

名前変数の値でシナリオを分岐します。
名前変数 `%a` の値が `みどり` であれば、ラベル `MIDORI` にジャンプします。
デフォルトの名前のときだけイベントを発生させるときに使えます。

```
@if %a == みどり MIDORI
名前がみどり以外です。
ここにシナリオを記述します。
@goto end

:MIDORI
名前がみどりです。
ここにシナリオを記述します。

:end
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="load">
## @load

```
@load [ファイル名] [ラベル名]
@load [file=ファイル名] [label=ラベル名]
```

別なシナリオファイルにジャンプします。
シナリオファイルはフォルダ `txt` の中に存在する必要があります。

### 使い方1

シナリオファイル `001.txt` にジャンプします。

```
@loa 001.txt
```

### 使い方2

シナリオファイル `001.txt` のラベル `AAA` にジャンプします。

```
@load 001.txt AAA
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="chapter">
## @chapter

```
@chapter "章タイトル"
@chapter title="章タイトル"
```

章のタイトルを設定します。
設定されたタイトルはセーブデータの項目に反映されます。

### 使い方

```
@chapter "第一章"
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="gosub">
## @gosub

```
@gosub [ラベル名]
```

マクロのラベルにジャンプします。

### マクロの書き方

マクロはラベルから始まり [@return](#return) で終わります。

### マクロの入れ子

マクロの中でマクロを呼び出すことはできません。

### マクロをファイルにまとめる

マクロはファイルにまとめておいて、[using] で取り込むことができます。

### 使い方1

`マクロ1` を実行します。

```
@gosub マクロ1
...

:マクロ1
ここに何らかの処理やメッセージを記述します。
@return
```

### 使い方2

省略記法があります。`&1` のように引数が使えます。

```
&キャラ表示 sample.png
...

:キャラ表示
@ch center &1 0.5
@return
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="return">
## @return

```
@return
```

マクロから戻ります。

### 使い方1

マクロ <code>SUB</code> を呼び出したと、元の位置に戻ります。

```
@gosub SUB

:SUB
ここに何らかの処理やメッセージを記述します。
@return
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="config">
## @config

```
@config [コンフィグ名] (値)
```

コンフィグをゲーム実行中に書き換えられる便利コマンドです。

### 使い方

メッセージボックスを変更します。
例えば、テキストを画面いっぱいに表示するノベルスタイルと、画面の下の方に表示するアドベンチャー形式を切り替えるのに使います。

```
@config msgbox.image msgbox2.png
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="wms">
## @wms

```
@wms [ファイル名]
@wms [file=ファイル名.txt]
```

WMS スクリプトを実行します。

### WMSとは

WMS は OpenNovel.org が開発したプログラミング言語です。
シナリオで表現しづらい複雑なロジックを記述するために、シナリオから呼び出して使用します。

### 使い方

`hello.txt` を実行します。

```
@wms hello.txt
```

<!---------------------------------------------------------------------------------------------------->
<hr>
<a name="using">
## using

```
using [マクロファイル名]
```

`using` を記述した行の位置に、外部ファイルの内容が挿入されます。
外部ファイルにまとめたマクロ一式を取り込むのに使用します。
マクロ内で定義したサブルーチンは通常の `@gosub` コマンドで呼び出すことができます。

### 共通処理の外部ファイル化

複数のシナリオファイルから呼び出される共通の処理を、1つのマクロファイルにまとめる方法について説明します。

マクロファイルに `共通処理1` というマクロを作り、ファイル `txt/macro.txt` とすることにします。

```
:共通処理1
ここに処理を書きます。
@return
```

これをシナリオファイルから呼ぶには、各シナリオファイルの末尾に、下記を追加します。
`@goto` を使っているのは、`using` の位置に展開されたマクロの内容が、そのままシナリオとして実行されないためのガードです。

```
@goto MACRO_END
using macro.txt
:MACRO_END
```

### 使い方

`macro.txt` を取り込んだ上で呼び出しす。

```
...
&共通処理1
...
...
...
@goto MACRO_END
using macro.txt
:MACRO_END
```
