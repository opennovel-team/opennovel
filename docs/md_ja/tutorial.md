OpenNovel チュートリアル
========================

このチュートリアルでは、OpenNovelを使ってゲームを作成する方法を説明します。
内容はとてもかんたんで、1時間もあれば取り組めます。

## セットアップ編

ここではWindowsを使います。最新のファイルをダウンロードして展開しておきます。

## ゲームフォルダを確認する

ゲームフォルダを開いてみます。
それぞれのフォルダがゲームの中身です。ファイルの振り分けは下記のようになっています。

|名前        |役割                                           |
|------------|-----------------------------------------------|
|txt         |シナリオファイル                               |
|bg          |背景の画像ファイル                             |
|ch          |キャラの画像ファイル                           |
|rule        |ユニバーサルトランジションのルール画像ファイル |
|cg          |その他の画像ファイル                           |
|bgm         |BGMの音声ファイル                              |
|se          |効果音の音声ファイル                           |
|cv          |ボイスの音声ファイル                           |
|anime       |アニメのスクリプトファイル                     |
|gui         |GUIのスクリプトファイル                        |
|font        |フォントファイル                               |
|wms         |WMSのスクリプトファイル                        |
|mov         |動画ファイル                                   |
|project.txt |設定ファイル                                   |

## 背景を表示する方法

最初に実行されるシナリオファイルは `txt` フォルダの `init.txt` というファイルです。
ここで一度、テキストの中身をすべて削除して、次の内容を貼り付けて保存し、実行してみてください。

```
@bg roof.png 1.0
```

屋上の背景が表示されるはずです。

* `@bg` というのが背景を表示する命令です。
* `roof.png` は `bg` フォルダの中のファイル名です。
* `1.0` は画面の表示が完了するまでの時間(秒)です。

## キャラを表示する方法

背景を表示できたところで、次はキャラクタの表示に移ります。
`init.txt` の中身をすべて削除して、次の内容を貼り付け、実行します。

```
@bg roof.png 1.0
@ch center midori/normal.png 1.0
```

屋上の背景が表示され、続いてキャラクタが表示され、クリックを行うと、終了するはずです。

* `@ch` というのがキャラクタを表示するコマンドです。
* `center` は表示位置(中央)です。
* `midori/normal.png` は `ch` フォルダの中のファイル名です。
* `1.0` はフェードインの時間(秒)です。

## メッセージとセリフを表示する方法

背景、キャラクタを表示できたところで、次はメッセージとセリフの表示に移ります。
`init.txt` の中身をすべて削除し、次の内容を貼り付け、実行してみてください。

```
@bg roof.png 1.0
@ch center midori/normal.png 1.0

メッセージボックスにテキストを表示するのが"メッセージ"です。

なまえ「キャラクタの名前を付けたメッセージが"セリフ"です。」

*なまえ*001.ogg*セリフにはボイスをつけることができます。
```

先ほどのキャラクタが表示されたあと、メッセージとセリフが表示されるはずです。

* 名前付きのセリフとして認識される行は、先頭が `*` で始まる行か、`名前「メッセージ」`の形式の行です。
* 名前の付かないメッセージとして認識される行は、普通の文章の行(先頭が `@` や `*` や `:` ではない行)です。

## 音楽を再生する方法

メッセージ、セリフを表示できたところで、次はBGMの再生に移ります。
`init.txt` の中身をすべて削除して、次の内容を貼り付け、実行してみてください。

```
@bgm 01.ogg
@bg roof.png 1.0
@ch center midori/normal.png 1.0
```

BGMが再生され、先ほどのキャラクタが表示されるはずです。

* `@bgm` はBGM再生コマンドを表します。
* `01.ogg` は `bgm` フォルダのファイル名を表します。

再生できるファイルは、44.1kHzのOgg Vorbis形式です。ステレオ、モノラルのどちらでも構いません。

## 選択肢を表示する方法

BGMの再生ができたところで、次は選択肢の表示に移ります。
`init.txt` の中身をすべて削除して、次の内容を貼り付け、実行してみてください。

```
@bg roof.png 1.0
@ch center midori/normal.png 1.0
さて、学校へ行くか。
@choose WALK 歩いて行こう BIKE 自転車で行こう
:WALK
歩いて行くことにした。
@goto end
:BIKE
自転車で行くことにした。
@goto end
:end
```

コピペしたら、ゲームを実行します。2つの選択肢が表示されるはずです。
`@choose` は選択肢命令を表し、`WALK`, `BIKE` は選択肢が選ばれたときにジャンプする先のラベルです(後述)。
ラベル名と選択肢のテキストが一組のセットです。
`:WALK` は、ジャンプ先として指定されるラベルです。
ラベルの行は、ゲームの見た目の上では何も実行されません。

## より便利な書き方

別な書き方として、次のようなスタイルもあります。
この場合、ラベルは省略できます。
お好みの方法を使ってください。

```
@bg roof.png 1.0
@ch center midori/normal.png 1.0

さて、学校へ行くか。

<<<
switch "歩いて行こう" "自転車で行こう" {
case "歩いて行こう":
    歩いて行くことにした。
    break
case "自転車で行こう":
    自転車で行くことにした。
    break
}
>>>
```

## シナリオを分岐させる方法

選択肢の表示ができたところで、次はフラグを立てて分岐する処理に移ります。
これができればゲームがひとまず形になります。
`init.txt` の中身をすべて削除して、次の内容を貼り付け、実行してみてください。

```
@bg roof.png 1.0
@ch center midori/normal.png 1.0

さて、学校へ行くか。

<<<
switch "歩いて行こう" "自転車で行こう" {
case "歩いて行こう":
    歩いて行くことにした。
    @set $1 = true
    break
case "自転車で行こう":
    自転車で行くことにした。
   break
}
>>>

@if $1 == false NO_MONEY

お金を拾った。

:NO_MONEY
```

保存したら、ゲームを実行します。
「歩いて行こう」を選択した場合だけ、「お金を拾った。」が表示されるはずです。

## 分岐の仕組み

ここでは `@set` と `@if` を組み合わせています。

まず `@set` について説明します。

* `@set` は、フラフを立てるコマンドです。
* 続く `$1` は、変数番号 `1` に値 `true` をセットすることを意味します。
* 続く `=` は、単純な代入であることを意味します。(他にも計算ができます)
* `true` は、整数の `1` を代入することを意味します。
*  変数の初期値は `0`、つまり `false` ですので、「歩いて行こう」が選択されたときだけ、変数 `$1` の値が `true` になります。

次に `@if` について説明します。

* `@if` は、条件分岐です。
* `$1 == false` は、変数 `$1` の値が `false`、つまり `0` の場合、という条件を意味します。
* `NO_MONEY` は、ラベルの名前です。条件に一致したとき、`:NO_MONEY` という行にジャンプします。
* 変数の初期値は `0`、つまり `false` ですので、結果として、「歩いて行こう」を選択した場合だけ、「お金を拾った。」が表示されることになります。

変数の使い方は0/1やfalse/trueのフラグだけではなく、好感度などの数値を持つこともできます。
詳細はコマンド・リファレンスの @set と @if をご覧ください。
ただ、スクリプトをシンプルに保つためには、なるべく0/1のフラグとして運用する方が望ましいと思います。

## より便利な書き方

ちなみに、`@if` を便利にした書き方もあります。拡張if文と呼ばれています。
ラベルやジャンプがよくわからないときは、この書き方を使ってみてください。

```
<<<
if $1 == 0 {
  お金を拾った。
}
>>>
```

この書き方では、`<<<` から `>>>` までの区間で拡張構文が有効になります。
途中で `<<<` が再び現れても、それはただのメッセージとして表示されてしまいますのでご注意ください。

## メニュー画面を表示させる方法

シナリオの分岐ができたところで、 ちょっとステップアップして、 タイトル画面やメニュー画面の作成方法を説明します。
`init.txt` の中身をすべて削除して、次の内容を貼り付け、実行してみてください。

```
@gui title.txt
:START
:LOAD
:CONFIG
:QUIT
```

ゲームを実行すると、タイトル画面が表示されるはずです。
詳しく見ていきます。

`@gui title.txt` というコマンドは、メニュー画面(GUIと呼ばれる)を表示するコマンドです。
これは `gui` フォルダの `title.txt` というファイルで設定されているメニュー画面を表示しています。
`title.txt` はGUIファイルと呼ばれています。

メニューファイルの書き方については、[GUI](gui.html) を参照 してください。
何やら難しそうと思われるかもしれませんが、画像編集ソフトのレイヤーを理解していれば、とても簡単に使えます。

## シナリオファイルを分割する方法

シナリオの分量が大きくなってくると、1つのシナリオファイルで書くのが大変になります。
そこで、シナリオの章やシーンごとにシナリオファイルを分割する方法があります。
`txt` フォルダの中に、`init.txt` のコピーを作成して、`second.txt` と名付けてください。

次の内容を貼り付け、保存してみてください。

* init.txt:
```
@bg roof.png 1.0
@ch center midori/normal.png 1.0

init.txtの中にいます。

@load second.txt
```

* second.txt:
```
second.txtに移動しました。
```

保存したら、ゲームを実行してみてください。
`second.txt` に移動できたことがわかるはずです。

## 以上です

基本的にはたったこれだけです！

ここで触れなかった内容には下記があります。

* 設定ファイル `project.txt`
* [GUI](gui.html)
* [アニメ](anime.html)

設定ファイルは見ればわかる内容ですので安心してください。
GUIとアニメはリンク先を読んでみてください。
